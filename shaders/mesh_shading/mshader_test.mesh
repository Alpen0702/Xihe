#version 450
#extension GL_EXT_mesh_shader : require

layout(local_size_x = 1, local_size_y = 1, local_size_z = 1) in;
layout(triangles, max_vertices = 126, max_primitives = 42) out;

layout(set = 0, binding = 2) uniform GlobalUniform {
    mat4 model;
    mat4 view_proj;
    vec3 camera_position;
} global_uniform;

struct s_vertex
{
	vec4 position;
	vec4 normal;
};

layout (std430, binding = 4) buffer _vertices
{
  s_vertex vertices[];
} vb;

struct s_meshlet
{
  uint vertices[64];
  uint indices[372];
  uint vertex_count;
  uint index_count;
};

layout (std430, binding = 3) buffer _meshlets
{
  s_meshlet meshlets[];
} mbuf;

layout (location = 0) out PerVertexData
{
  vec4 color;
  vec3 normal;
} v_out[];

uint find_local_index(uint global_index, uint vertex_count, uint vi_array[64]) {
    for (uint j = 0; j < vertex_count; ++j) {
        if (vi_array[j] == global_index)
            return j;
    }
    // Handle error case if not found
    return 0;
}

void main(void)
{
    uint meshlet_index = gl_WorkGroupID.x;
    uint vertex_count = mbuf.meshlets[meshlet_index].vertex_count;
    uint index_count = mbuf.meshlets[meshlet_index].index_count;
    uint primitive_count = index_count / 3;

    SetMeshOutputsEXT(vertex_count, primitive_count);

    mat4 MVP = global_uniform.view_proj * global_uniform.model;

    vec4 meshlet_color = vec4(
        float((meshlet_index * 37) % 255) / 255.0, // R component
        float((meshlet_index * 73) % 255) / 255.0, // G component
        float((meshlet_index * 151) % 255) / 255.0, // B component
        1.0 // Alpha component
    );

    // Output each vertex's position and color
    for (uint i = 0; i < vertex_count; ++i)
    {
        // Directly use local indices for vertex access
        uint local_vi = mbuf.meshlets[meshlet_index].vertices[i];
        gl_MeshVerticesEXT[i].gl_Position = MVP * vb.vertices[local_vi].position;
        v_out[i].color = meshlet_color;
        v_out[i].normal = normalize(mat3(global_uniform.model) * vb.vertices[local_vi].normal.xyz);
    }

    // Set each triangle's vertex indices using local indices
    for (uint i = 0; i < primitive_count; ++i)
    {
        uint local_idx0 = mbuf.meshlets[meshlet_index].indices[i * 3 + 0];
        uint local_idx1 = mbuf.meshlets[meshlet_index].indices[i * 3 + 1];
        uint local_idx2 = mbuf.meshlets[meshlet_index].indices[i * 3 + 2];

        gl_PrimitiveTriangleIndicesEXT[i] = uvec3(local_idx0, local_idx1, local_idx2);
    }
}
